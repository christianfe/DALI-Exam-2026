%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% AMBULANCE agent 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

:- write('I\'m the ambulance agent').

:- dynamic status/1.
:- dynamic busy/1.
:- dynamic current_job/1.

status(available).     % available | unavailable
busy(no).              % yes | no

dispatcher(dispatcher).

% calcolo probabilità data percentuale
prob(Perc) :- random_between(0,99,R), R < Perc.

%%%%%%%%%%%%%%%%%%%%
% EVENTI ESTERNI (E)
%%%%%%%%%%%%%%%%%%%%

% Dispatch arriva dal Dispatcher
dispatchE(_Loc) :>
    true.

% Avvistamento incendio dall'ambiente
spot_fireE(_Loc) :>
    true.

%%%%%%%%%%%%%%%%%%%%
% EVENTI PRESENTI (N)
%%%%%%%%%%%%%%%%%%%%

% Accettazione/rifiuto dispatch
accept_dispatch(Loc) :-
    dispatchN(Loc),
    status(available),
    busy(no).

refuse_dispatch(Loc) :-
    dispatchN(Loc),
    status(unavailable).

refuse_dispatch(Loc) :-
    dispatchN(Loc),
    status(available),
    busy(yes).

% Proattività: se vedo incendio, chiedo supporto a dispatcher
need_firerescue(Loc) :-
    spot_fireN(Loc).

do_rescue(Loc) :-
    status(available),
    busy(yes),
    current_job(Loc).

end_shift :-
    do_rescueP(_Loc),
    status(available),
    prob(10).

wakeup :-
    status(unavailable),
    prob(30).

%%%%%%%%%%%%%%%%%%%%
% EVENTI INTERNI (I)
%%%%%%%%%%%%%%%%%%%%

start :- true.

startI :>
    retractall(status(_)),
    assert(status(available)),
    retractall(busy(_)),
    assert(busy(no)),
    retractall(current_job(_)),
    dispatcher(D),
    messageA(D, send_message(ambulance_status(available), ambulance)).

% Accetto dispatch: set busy + job, comunico e navigo
accept_dispatchI(Loc) :>
    retractall(busy(_)),
    assert(busy(yes)),
    retractall(current_job(_)),
    assert(current_job(Loc)),
    dispatcher(D),
    messageA(D, send_message(ambulance_ack(dispatch,Loc), ambulance)),
    navigate_toA(Loc).

% Rifiuto dispatch: comunico motivo (unavailable o busy)
refuse_dispatchI(Loc) :>
    status(unavailable),
    dispatcher(D),
    messageA(D, send_message(dispatch_refused(Loc, reason(unavailable)), ambulance)).

refuse_dispatchI(Loc) :>
    status(available),
    busy(yes),
    current_job(Cur),
    dispatcher(D),
    messageA(D, send_message(dispatch_refused(Loc, reason(busy(Cur))), ambulance)).

% Incendio: chiedo FireRescue
need_firerescueI(Loc) :>
    dispatcher(D),
    messageA(D, propose(dispatch(firerescue,Loc), [], ambulance)).

% Rescue: azione minima + report, poi libero il job
do_rescueI(Loc) :>
    rescue_peopleA,
    reportA(emergency_retired),
    dispatcher(D),
    messageA(D, send_message(report(emergency_retired,Loc), ambulance)),
    retractall(busy(_)),
    assert(busy(no)),
    retractall(current_job(_)).

% Fine turno con 10% dopo un intervento
end_shiftI :>
    retractall(status(_)),
    assert(status(unavailable)),
    dispatcher(D),
    messageA(D, send_message(ambulance_status(unavailable), ambulance)).

% Wakeup (30%): torno disponibile
wakeupI :>
    retractall(status(_)),
    assert(status(available)),
    dispatcher(D),
    messageA(D, send_message(ambulance_status(available), ambulance)).

%%%%%%%%%%%%%%%%%%%%
% AZIONI (A)
%%%%%%%%%%%%%%%%%%%%

navigate_toA(_Loc) :-
    true.

rescue_peopleA :-
    true.

reportA(emergency_retired) :-
    true.